plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "1.2.51"
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id "com.github.breadmoirai.github-release" version "2.0.1"
}

group = 'atm.bloodworkxgaming'
version = '1.2.4'

sourceCompatibility = 1.8

repositories {
    mavenCentral()

    maven {
        name = 'DVS1'
        url = 'http://dvs1.progwml6.com/files/maven'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile group: 'org.yaml', name: 'snakeyaml', version: '1.21'


    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile 'com.squareup.okhttp3:okhttp:3.10.0'

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.4'

    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")

    // https://mvnrepository.com/artifact/org.fusesource.jansi/jansi
    compile group: 'org.fusesource.jansi', name: 'jansi', version: '1.17.1'
    compile 'org.apache.commons:commons-lang3:3.7'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.20'
}

shadowJar {
    classifier = ""
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'atm.bloodworkxgaming.serverstarter.ServerStarterKt'
        )
    }
}


tasks.build.dependsOn shadowJar


task copyJar(type: Copy) {
    dependsOn build


    from file("$buildDir/libs/serverstarter-${version}.jar")
    into file("$buildDir/dist/serverstarter-$version/")
}

task packageDist(type: Copy) {
    group = "build"
    dependsOn copyJar

    from file("src/main/resources/startserver.bat")
    from file("src/main/resources/startserver.sh")
    from file("server-setup-config.yaml")


    into file("$buildDir/dist/serverstarter-$version/")

    filter { it.replaceAll("@@serverstarter-libVersion@@", version as String) }
}

task zipDist (type: Zip) {
    dependsOn packageDist
    group = "build"

    from file("$buildDir/dist/serverstarter-$version/")
    include '*'
    include '*/**'
    archiveName = "serverstarter-${version}.zip"
    destinationDir = file("$buildDir/release/")
}


githubRelease {
    token = getGithubKey()
    owner = "AllTheMods"
    repo = "ServerStarter"

    releaseAssets file("$buildDir/release/serverstarter-${version}.zip")
}

tasks.githubRelease.dependsOn zipDist

static String getGithubKey(){
    if (new File('secrets.properties').exists()) {

        Properties props = new Properties()
        props.load(new FileInputStream(new File('secrets.properties')))
        return props['GITHUB_TOKEN']
    }

    return ""
}

task depsize {
    group = "help"
    doLast {
        def size = 0
        def formatStr = "%,10.2f"
        configurations.default.collect { it.length() / (1024 * 1024) }.each { size += it }

        def out = new StringBuffer()
        out << 'Total dependencies size:'.padRight(45)
        out << "${String.format(formatStr, size)} Mb\n\n"

        configurations
                .default
                .sort { -it.length() }
                .each {
            out << "${it.name}".padRight(45)
            out << "${String.format(formatStr, (it.length() / 1024))} kb\n"
        }
        println(out)
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}